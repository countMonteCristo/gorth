include "std/std.gorth"

const N 100 end

alloc array N 1 + end

func main
  : int
do
  1 array N 1 - ptr+ !8

  0 while dup N < do
    0 while dup N < do
      dup array swap ptr+ @8
      if 1 = do '*' else ' ' end
      putc
      1 +
    end drop
    '\n' putc

    1 while dup N <= do
      dup dup

      dup 1 - array swap ptr+ @8 swap
      dup array swap ptr+ @8 swap
      dup 1 + array swap ptr+ @8 swap drop

      over over * rot rot + swap rot over * + +
      2 %

      swap array swap ptr+ -1 ptr+ !8

      1 +
    end drop

    N while dup 0 > do
      dup dup 1 - array swap ptr+ @8 swap array swap ptr+ !8
      1 -
    end drop
    0 array !8

    1 +
  end drop
  0
end
