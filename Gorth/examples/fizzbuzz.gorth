include "std/std.gorth"

// Famous FizzBuzz problem: for every number x in range [1, 100] print this
// number and corresponding string.
// The string should be:
//   * 'Fizz' if x is divisible only by 3 and not by 5
//   * 'Buzz' if x is divisible only by 5 and not by 3
//   * 'FizzBuzz' if x is divisible by both 3 and 5
//   * '' - otherwise

const N 100 end

func divisible
  int int : bool
do
  % 0 =
end

func main
  : int
do
  1 while dup N <= do
    capture x int do
      x puti ' ' putc

      if x 3 divisible do
        "Fizz" puts
      end
      if x 5 divisible do
        "Buzz" puts
      end
      '\n' putc

    end
    1 +
  end drop
  0
end
